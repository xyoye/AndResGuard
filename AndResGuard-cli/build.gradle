apply plugin: 'java'

[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

version rootProject.ext.VERSION_NAME
group rootProject.ext.GROUP

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])
  //implementation group: 'com.tencent.mm', name: 'AndResGuard-core', version: version
  implementation project(':AndResGuard-core')
}

sourceSets {
  main {
    java {
      srcDir 'src'
    }
  }
}

jar {
  manifest {
    attributes 'Main-Class': 'com.tencent.mm.resourceproguard.cli.CliMain'
    attributes 'Manifest-Version': version
    attributes "Jar-Version": "${ANDRESGUARD_VESSION}"
    attributes "Build-Time": releaseTime()
  }

  configurations.implementation.setCanBeResolved(true)
  from {
    configurations.implementation.collect { it.isDirectory() ? it : zipTree(it) }
  }

  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
  exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA'
}

// copy the jar to work directory
task buildJar(type: Copy, dependsOn: [build, jar]) {
  from('build/libs') {
    include '*' + version + '*.jar'
  }
  into('../tool_output')
}

def releaseTime() {
  return new Date().format("yyyy-MM-dd HH:mm ZZZ", TimeZone.getDefault())
}

defaultTasks 'buildJar'
